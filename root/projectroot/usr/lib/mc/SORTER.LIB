<?com_websocket(55003)?>


dim shared this_scanDistance	as const long		= 80

dim shared this_placePos		as const location of xyzr	= #{52, 59, -26, 0}
dim shared this_placePart		as long

dim shared this_nextScanPos as double


<?lib_start()?>
	data_ppmReset()
	this_nextScanPos = 0
	this_placePart = 0
end sub


<?lib_prgStart()?>
	conv_enableAttach()
	extra_enableAttach()
	delta1_enableAttach()
	tool_select(tool_typeVacuum)
	masterFrame = conv_frame
	jog conv 80
	
	'delta1_torque(30)
	vrate = 50
	arate = 50
	drate = 50
end sub


<?lib_prgEvent(1)?>
	if conv.positionCommand>=this_nextScanPos then
		this_nextScanPos = conv.positionCommand + this_scanDistance
		camera_trigger()
		sleep 4
		com_clear()
		com_putDouble("/trigger", conv.positionCommand)
		com_send(this_websocket)
	end if
end sub


<?com_prgReceive('this_websocket', 1500)?>
	select case receive_
	case com_receiveMessage
		handleItem(com_getLocationXyzr("/pos"), com_getLong("/color"))
	end select
end sub


<?lib_prgError()?>
	sorter_stopTrigger()
	try
		idletask sorter.prg
	catch else
	end try
end sub

<?lib_prgStop()?>
	allDisable()
	masterFrame = NONE
	tool_select(tool_typeNone)
end sub


sub handleItem(byval pos_ as location of xyzr, byval color_ as long)
	if (conv.positionCommand+conv_frame.downMaster[1]-pos_{1})>120 then
		trigger delta1 ndof=1 value=pos_{1} masterFrame=conv_frame
		fetchItem(pos_, color_)
		data_ppmPlace()
	end if
end sub

sub fetchItem(byval pos_ as location of xyzr, byval color_ as long)
	slave = 5
	moves conv_frame.zero:#{0, pos_{2}, pos_{3}-5, -conv_frame.zero{4}}
	move extra pos_{4} +conv_frame.downStream[1]{4} -135
	waitForMotion
	while not isMovingFrameSynchronized>0
		sleep 1
	end while
	
	moves #{0, 0, +5, 0} abs=false
	waitForMotion
	tool_grip(true)
	sleep 300
	moves #{0, 0, -10, 0} abs=false
	waitForMotion
	slave = off
	
	machineTable = extra_machineTable
	moves this_placePos - #{int(this_placePart/5)*35, (this_placePart mod 5)*30, 0,0}
	increment(this_placePart)
	waitForMotion extra
	moves #{0, 0, +7, 0} abs=false vrate=5
	tool_grip(false)
	moves #{0, 0, -7, 0} abs=false
	'move extra +2 abs=false
	'move extra -4 abs=false
	'move extra +2 abs=false
	waitForMotion
	machineTable = #{0,0,0,0}
end sub



function anyMoving as long
	anyMoving = delta1.isMoving or conv.isMoving or extra.isMoving
end function

sub allStop
	while anyMoving
		stop delta1
		stop extra
		stop conv
		sleep 16
	end while
end sub

sub allDisable
	allStop()
	delta1_disable()
	extra_disable()
	conv_disable()
end sub
