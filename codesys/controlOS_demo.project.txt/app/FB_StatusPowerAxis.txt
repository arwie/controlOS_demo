// SPDX-FileCopyrightText: 2025 Artur Wiebe <artur@4wiebe.de>
// SPDX-License-Identifier: MIT

FUNCTION_BLOCK FB_StatusPowerAxis
VAR_INPUT
	Axis:		REFERENCE TO AXIS_REF_SM3;
	power:		BOOL;
END_VAR
VAR_OUTPUT
	Status:		MC_ReadStatus;
	powered:	BOOL;
	moving:		BOOL;
	error:		BOOL;
END_VAR
VAR
	AxisReset:	MC_Reset;
	AxisPower:	MC_Power;
	PoweredTON:	TON;
	resetEn:	RS;
END_VAR

////////////////////////////////

Status(
	Axis := Axis,
	Enable := TRUE,
);

moving := NOT (Status.StandStill OR Status.Errorstop OR Status.Disabled);


AxisPower.bRegulatorOn := power OR moving;
AxisPower(
	Axis := Axis,
	Enable := TRUE,
	bDriveStart := AxisPower.bRegulatorOn,
);

PoweredTON(
	IN := AxisPower.Status,
	PT := T#40MS,
);

powered := PoweredTON.Q AND (powered OR NOT Status.Errorstop);


resetEn(
	SET := power,
	RESET1 := powered,
);

AxisReset(
	Axis := Axis,
	Execute := resetEn.Q1 AND Status.ErrorStop,
	Error => error,
);
