// SPDX-FileCopyrightText: 2025 Artur Wiebe <artur@4wiebe.de>
// SPDX-License-Identifier: MIT

FUNCTION_BLOCK FB_StatusPowerGroup
VAR_INPUT
	Group:		REFERENCE TO AXIS_GROUP_REF_SM3;
	power:		BOOL;
END_VAR
VAR_OUTPUT
	Status:		MC_GroupReadStatus;
	powered:	BOOL;
	moving:		BOOL;
	error:		BOOL;
END_VAR
VAR
	GroupReset: MC_GroupReset;
	GroupPower: SMC_GroupPower;
	PoweredTON:	TON;
	resetEn:	RS;
END_VAR

////////////////////////////////

Status(
	AxisGroup := Group,
	Enable := TRUE,
);

moving := NOT (Status.GroupStandby OR Status.GroupErrorStop OR Status.GroupDisabled);


GroupPower.bRegulatorOn := power OR moving;
GroupPower(
	AxisGroup := Group,
	Enable := TRUE,
	bDriveStart := GroupPower.bRegulatorOn,
);

PoweredTON(
	IN := GroupPower.Status,
	PT := T#40MS,
);

powered := PoweredTON.Q AND (powered OR NOT Status.GroupErrorStop);


resetEn(
	SET := power,
	RESET1 := powered,
);

GroupReset(
	AxisGroup := Group,
	Execute := resetEn.Q1 AND Status.GroupErrorStop,
	Error => error,
);
